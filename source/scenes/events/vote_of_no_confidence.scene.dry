title: The Right calls a vote of no confidence against the current government!
subtitle: We have angered our coalition partners too much...
new-page: true
view-if: spd_in_government = 1 and coalition_dissent >= 3 and in_grand_coalition = 1
tags: event

# Options: spend relations to reduce coalition_dissent, ask for kpd to abstain
# (lmao KPD toleration arc...)

= Vote of No Confidence

The DVP has called for a vote of no confidence against us, due to our various policies and disagreements. If this succeeds, there will be an election in a few months.

- @try_assuage: Try to assuage our partners.
- @support_kpd: Could we get the KPD to abstain?
- @let_it_happen: Let the vote happen.

@try_assuage

How do we assuage our partners?

- @give_up_prussia: Give up on leadership of Prussia.
- @austerity: Enact a massive austerity plan.
- @resources: Use our resources to reduce dissent within the coalition.
- @support_kpd: Could we get the KPD to abstain?
- @let_it_happen: Never mind. Let the vote happen.

@give_up_prussia
view-if: spd_prussia == 1
on-arrival: coalition_dissent = 0; spd_prussia = 0; prussian_government = "I"; reformist_dissent += 10; centrist_dissent += 10; labor_dissent += 10; left_dissent += 5; workers_spd -= 5; rural_spd -= 5; 

Fine. We no longer control the government of Prussia. The vote has been called off.

@austerity
view-if: welfare >= 0
on-arrival: coalition_dissent = 0; workers_spd -= 10; umemployed_spd -= 10; workers_kpd += 7; workers_nsdap += 3; unemployed_kpd += 5; unemployed_nsdap += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 3

Fine. We enacted a massive austerity program to cut welfare, in order to maintain the stability of our coalition. The vote has been called off.

@resources
choose-if: resources >= 3
unavailable-subtitle: We do not have enough resources.
subtitle: -3 resources
on-arrival: resources -= 3; coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.

@support_kpd
choose-if: communist_coalition >= 2 and kpd_relation >= 50 and kpd_r >= dvp_r
unavailable-subtitle: They would never support us...

If the Communists abstain, then we essentially can maintain a minority government[? if president=="Hindenburg":, although it would be very difficult to make policy without the support of President Hindenburg, which we do not have ?]. If this happens, then we will own a massive debt to the KPD. This could lead to broader cooperation in the future, for better or for worse.

- @support_kpd_2: Persuade the KPD to "tolerate" us?
- @let_it_happen: Never mind. Let the vote happen.

@support_kpd_2
on-arrival: communist_coalition += 1; workers_kpd += 3

The KPD is not interested in a "toleration" arrangement; they will only support us if they can participate in the government as part of a coalition.

- @election_1928.popular_front_coalition: Form a "Popular Front" coalition?
- @election_1928.left_coalition
- @try_assuage: Never mind. We must deal with this some other way.



@let_it_happen
on-arrival: next_election_month = month + 3; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0; chancellor = "Brüning"; chancellor_party = "Z "; welfare -= 2; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "DDP"; finance_minister_party = "DVP"; agricultural_minister_party = "Z"; coalition_dissent = 0; welfare -= 2; pro_republic -= 4  

The vote succeeds, and our government has resigned. Chancellor Brüning now leads an all-bourgeois cabinet. He immediately proceeds to cut welfare spending substantially.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
