title: Game Over
new-page: true
go-to: eg_menu

= End


@eg_menu

- #endings
- @end_game: End game.

# TODO: achievements

@hitler_wins
view-if: (chancellor == "Hitler" or president == "Hitler") and (total_defeat==1 or civil_war_seen == 0)
title: Adolf Hitler is in undisputed control over Germany.
tags: endings
go-to: eg_menu

Adolf Hitler is in undisputed control over Germany. The SPD, along with all of the democratic forces, have failed.

[? if civil_war_seen == 1 : At the very least, we fought back. Perhaps our resistance will be celebrated in an impossible-to-forsee future. ?]

@war_against_hitler
view-if: (chancellor == "Hitler" or president == "Hitler") and (long_war == 1)
title: Adolf Hitler is in power, but we are fighting to stop him.
tags: endings
go-to: eg_menu

Adolf Hitler has taken power in Germany. However, we have rallied the democratic forces of the country against him, and are fighting back. The outcome of this war is uncertain.


@no_hitler
view-if: chancellor != "Hitler" and president != "Hitler"
title: Hitler does not yet control Germany...
tags: endings
go-to: eg_menu

Hitler does not yet control Germany. [? if chancellor == "Papen" or chancellor == "Schleicher" : However, Chancellor [+ chancellor +] is not much better. ?][? if resist_coup == 1 and republic_victory == 0 and long_war == 0 : Given our defeat in the Prussian coup, the far-right appears to be ascendant. ?][? if chancellor == "Brüning" : However, Chancellor Brüning's austerity policies and the continuation of the depression are paving the way for Hitler, or someone like him, to take power. ?]

[? if hitler_deported == 1 : We have deported Hitler back to Austria. Hopefully he will never return. ?]

@civil_war_won
title: We have won the civil war.
view-if: republic_victory == 1
tags: endings
go-to: eg_menu

We have won the civil war against the reactionary forces. Germany will be changed forever, but it is too early to know how.



@civil_war_lost
title: We lost the civil war.
view-if: total_defeat == 1
tags: endings
go-to: eg_menu

We have lost the civil war.

@long_war
title: Germany is gripped by civil war.
view-if: long_war == 1
tags: endings
go-to: eg_menu

Germany is currently in the grip of a civil war. It is quite possible that the outcome will only be determined by foreign intervention.

@braun_victorious
title: Otto Braun victorious
view-if: president == "Braun"
tags: endings
go-to: eg_menu

Otto Braun has been elected as President of Germany. He had never desired to be the most powerful man in Germany, but now he is. 

@spd_victorious
title: SPD still in government
view-if: spd_in_government == 1 and chancellor_party == "SPD" and total_defeat == 0 and long_war == 0
tags: endings
go-to: eg_menu

The SPD is still in government, under Chancellor [+ chancellor +]. Given the circumstances, this is a substantial accomplishment.

@communist_victory
title: Communist victory
view-if: chancellor_party == "KPD" or president == "Thälmann" and total_defeat == 0 and long_war == 0
tags: endings
go-to: eg_menu

The KPD has won power in Germany, with our assistance. They will try to remake Germany in the Soviet image. How much they succeed is yet to be determined.

@works_program
title: Works program enacted!
view-if: works_program > 0
tags: endings
go-to: eg_menu

Our government has enacted a public works program!

@unemployment_reduced
title: Unemployment has been successfully reduced!
view-if: unemployed <= 20
tags: endings
go-to: eg_menu

While still uncomfortably high, unemployment levels are nowhere near their peak. This is a success of our economic policy.


@unemployment_high
title: Unemployment is still too high.
view-if: unemployed >= 30 and (chancellor != "Hitler" and president != "Hitler") and (civil_war_seen == 0)
tags: endings
go-to: eg_menu

Unemployment is still at dangerously high levels. Even though Hitler is not in power, the situation is ripe for instability and extremism.


@emergency_government
title: SPD ruling in an emergency government.
view-if: in_emergency_government == 1

The SPD is ruling as part of an emergency minority government, led by President [+ president +] and Chancellor [+ chancellor +]. This is a temporary and unstable arrangement, leaving the door open to further authoritarianism.



@end_game
game-over: true
