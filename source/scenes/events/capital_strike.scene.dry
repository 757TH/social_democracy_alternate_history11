title: Capital Strike
subtitle: Germany's industries are idling...
view-if: capital_strike_progress >= 10 and spd_in_government
new-page: true
max-visits: 1
on-arrival: unemployed += 4; workers_spd*= 0.7; new_middle_spd -= 15; old_middle_spd -= 15; unemployed_spd*=0.7; rural_spd -= 15; pro_republic -= 10; capital_strike_seen = 1
tags: event

= Capital Strike

Reportedly, our policies have greatly angered the capitalists. Investment has fallen, and factories are idling, preventing the workers from fulfilling their jobs. Meanwhile, the bourgeois press is a constant chorus of venomous invective directed at us and our economic policies.

What can we do?

- @seize
- @capital_controls
- @propaganda
- @nothing

@seize
title: Empower workers to seize the factories!
choose-if: left_strength > reformist_strength + center_strength and judicial_reform >= 4
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: The bulk of the party does not support this action. ?] [? if judicial_reform < 4 : The judiciary would oppose this action. ?]
on-arrival: nationalization_progress += 1; coup_progress += 6; unemployed -= 2; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 6*(1-dissent); coalition_dissent += 2; dvp_relation -= 4; workers_nsdap -= 5*(1-dissent); unemployed_nsdap -= 5*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 5;

If the capitalists are going to attack us, then we must hit them back. We have empowered the workers to seize and take full control of the factories that the capitalists have idled. The fascists, of course, call us thieves and looters.


@capital_controls
title: Enact capital controls to lessen the impact.
choose-if: judicial_reform >= 2
unavailable-subtitle: The judiciary would never allow this.
on-arrival: unemployed -= 1.5; workers_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent);

Capital controls might blunt some of the worst economic components of the capital strike.



@propaganda
title: Launch a propaganda campaign blaming the capitalists for the crisis.
subtitle: -1 resources
choose-if: (radio >= 2 or commercialized_media >= 3) and resources >= 1
unavailable-subtitle: [? if radio < 2 and commercialized_media < 3 : We have not built up our media enough. ?] [? if resources < 1: We do not have enough resources. ?]
on-arrival: workers_spd += 7*(1-dissent); new_middle_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); pro_republic += 5*(1-dissent); resources -= 1

We launch a counter-propaganda campaign to blame the economic crisis on the capitalists, using all of our media channels to do so. Because of our existing media framework, this is moderately successful in directing popular criticisms towards the capitalists and away from us.


@nothing
title: There is nothing we can do.

There is nothing we can do that is within constitutional and legal bounds.
